plugins {
    id "org.springframework.boot" version "2.6.4"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id 'nu.studer.jooq' version '6.0.1'
    id "java"
}

group = "web-socket-chat"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = "11"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    // general development support library
    commons_collection_version = "4.4"
    commons_lang_version = "3.12.0"
    springdoc_openapi_version = "1.6.6"

    // mariadb connector
    mariadb_connector_version = "3.0.4"
}

dependencies {
    // spring boot starter dependencies
    implementation "org.springframework.boot:spring-boot-starter-aop"
    implementation "org.springframework.boot:spring-boot-starter-jdbc"
    implementation "org.springframework.boot:spring-boot-starter-jooq"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-websocket"

    // general development support library dependencies
    implementation "org.apache.commons:commons-lang3:$commons_lang_version"
    implementation "org.apache.commons:commons-collections4:$commons_collection_version"
    implementation "org.springdoc:springdoc-openapi-ui:$springdoc_openapi_version"

    implementation "org.mariadb.jdbc:mariadb-java-client:$mariadb_connector_version"
    jooqGenerator "org.mariadb.jdbc:mariadb-java-client:$mariadb_connector_version"

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation "org.springframework.boot:spring-boot-starter-test"

    testCompileOnly "org.projectlombok:lombok"
    testAnnotationProcessor "org.projectlombok:lombok"
}

jooq {
    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.ERROR
                jdbc {
                    driver = 'org.mariadb.jdbc.Driver'
                    url = 'jdbc:mariadb://13.209.4.83:3306'
                    user = 'dlrtn'
                    password = 'jj5514'
                }
                generator {
                    strategy {
                        name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                    }
                    database {
                        name = 'org.jooq.meta.mariadb.MariaDBDatabase'
                        includes = 'study_db.*'
                        excludes = ''
                    }
                    target {
                        packageName = 'com.dlrtn.websocket.chat.business.chat.model.domain.generated'
                    }
                }
            }
        }
    }
}

test {
    useJUnitPlatform()
}
